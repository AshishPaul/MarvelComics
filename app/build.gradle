apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "in.zerogravity.marvelcomics"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
            buildConfigField 'String', 'API_BASE_URL', '"http://gateway.marvel.com"';
        }
        prod {
            dimension "default"
            buildConfigField 'String', 'API_BASE_URL', '"http://gateway.marvel.com"';
        }
    }
    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$junitVersion"
    implementation "com.android.support:appcompat-v7:$supportLibrayVersion"
    implementation "com.android.support:support-v4:$supportLibrayVersion"
    implementation "com.android.support:design:$supportLibrayVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibrayVersion"
    implementation "com.android.support:cardview-v7:$supportLibrayVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    annotationProcessor "com.google.auto.value:auto-value:$autoValueGoogleVersion"
    compileOnly "com.google.auto.value:auto-value:$autoValueGoogleVersion"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson:0.6.0'
    compileOnly 'com.ryanharter.auto.value:auto-value-gson-annotations:0.6.0'
    implementation 'com.roughike:bottom-bar:2.3.1'
}
